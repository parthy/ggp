(role red)
(role black)
(init (placecontrol red))
(<= (legal ?player (place ?q ?x ?y))
    (role ?player)
    (true (placecontrol ?player))
    (cellempty ?q ?x ?y))
(<= (legal ?player (rotate ?q ?dir))
    (quadrant ?q)
    (direction ?dir)
    (role ?player)
    (true (rotatecontrol ?player)))
(<= (legal ?player noop)
    (role ?player)
    (not (true (placecontrol ?player)))
    (not (true (rotatecontrol ?player))))
(<= (next (cellholds ?q ?x ?y ?player))
    (role ?player)
    (does ?player (place ?q ?x ?y)))
(<= (next (cellholds ?q ?x ?y ?state))
    (true (cellholds ?q ?x ?y ?state))
    (role ?player)
    (does ?player (place ?qany ?xany ?yany)))
(<= (next (cellholds ?q ?x2 ?y2 ?state))
    (role ?player)
    (does ?player (rotate ?q ?dir))
    (true (cellholds ?q ?x1 ?y1 ?state))
    (rotation ?dir ?x1 ?y1 ?x2 ?y2))
(<= (next (cellholds ?q1 ?x ?y ?state))
    (role ?player)
    (does ?player (rotate ?q2 ?dirany))
    (true (cellholds ?q1 ?x ?y ?state))
    (distinct ?q1 ?q2))
(<= (next (rotatecontrol red))
    (true (placecontrol red)))
(<= (next (placecontrol red))
    (true (rotatecontrol black)))
(<= (next (rotatecontrol black))
    (true (placecontrol black)))
(<= (next (placecontrol black))
    (true (rotatecontrol red)))
(<= terminal
    (line red))
(<= terminal
    (line black))
(<= terminal
    (not boardopen))

(opponent red black)
(opponent black red)

(<= (goal ?p 100)
    (line ?p)
    (opponent ?p ?p2)
    (not (line ?p2)))
(<= (goal ?p 50)
	(role ?p)
    (not (line red))
    (not (line black))
    (not boardopen))
(<= (goal ?p 50)
	(role ?p)
    (line red)
    (line black))
(<= (goal ?p2 0)
    (line ?p)
    (opponent ?p ?p2)
    (not (line ?p2)))
(<= (goal ?p 0)
	(role ?p)
    (not (line red))
    (not (line black))
    boardopen)

(<= (line ?player)
    (row ?player))
(<= (line ?player)
    (col ?player))
(<= (line ?player)
    (diag ?player))
(<= (row ?player)
    (role ?player)
    (true (cellholds ?q1 ?x1 ?y1 ?player))
    (globalindex ?q1 ?x1 ?y1 ?x1g ?yg)
    (plusplus ?x1g ?x2g)
    (true (cellholds ?q2 ?x2 ?y2 ?player))
    (globalindex ?q2 ?x2 ?y2 ?x2g ?yg)
    (plusplus ?x2g ?x3g)
    (true (cellholds ?q3 ?x3 ?y3 ?player))
    (globalindex ?q3 ?x3 ?y3 ?x3g ?yg)
    (plusplus ?x3g ?x4g)
    (true (cellholds ?q4 ?x4 ?y4 ?player))
    (globalindex ?q4 ?x4 ?y4 ?x4g ?yg)
    (plusplus ?x4g ?x5g)
    (true (cellholds ?q5 ?x5 ?y5 ?player))
    (globalindex ?q5 ?x5 ?y5 ?x5g ?yg))
(<= (col ?player)
    (role ?player)
    (true (cellholds ?q1 ?x1 ?y1 ?player))
    (globalindex ?q1 ?x1 ?y1 ?xg ?y1g)
    (plusplus ?y1g ?y2g)
    (true (cellholds ?q2 ?x2 ?y2 ?player))
    (globalindex ?q2 ?x2 ?y2 ?xg ?y2g)
    (plusplus ?y2g ?y3g)
    (true (cellholds ?q3 ?x3 ?y3 ?player))
    (globalindex ?q3 ?x3 ?y3 ?xg ?y3g)
    (plusplus ?y3g ?y4g)
    (true (cellholds ?q4 ?x4 ?y4 ?player))
    (globalindex ?q4 ?x4 ?y4 ?xg ?y4g)
    (plusplus ?y4g ?y5g)
    (true (cellholds ?q5 ?x5 ?y5 ?player))
    (globalindex ?q5 ?x5 ?y5 ?xg ?y5g))
(<= (diag ?player)
    (role ?player)
    (true (cellholds ?q1 ?x1 ?y1 ?player))
    (globalindex ?q1 ?x1 ?y1 ?x1g ?y1g)
    (plusplus ?x1g ?x2g)
    (plusplus ?y1g ?y2g)
    (true (cellholds ?q2 ?x2 ?y2 ?player))
    (globalindex ?q2 ?x2 ?y2 ?x2g ?y2g)
    (plusplus ?x2g ?x3g)
    (plusplus ?y2g ?y3g)
    (true (cellholds ?q3 ?x3 ?y3 ?player))
    (globalindex ?q3 ?x3 ?y3 ?x3g ?y3g)
    (plusplus ?x3g ?x4g)
    (plusplus ?y3g ?y4g)
    (true (cellholds ?q4 ?x4 ?y4 ?player))
    (globalindex ?q4 ?x4 ?y4 ?x4g ?y4g)
    (plusplus ?x4g ?x5g)
    (plusplus ?y4g ?y5g)
    (true (cellholds ?q5 ?x5 ?y5 ?player))
    (globalindex ?q5 ?x5 ?y5 ?x5g ?y5g))
(<= (diag ?player)
    (role ?player)
    (true (cellholds ?q1 ?x1 ?y1 ?player))
    (globalindex ?q1 ?x1 ?y1 ?x1g ?y5g)
    (plusplus ?x1g ?x2g)
    (plusplus ?y4g ?y5g)
    (true (cellholds ?q2 ?x2 ?y2 ?player))
    (globalindex ?q2 ?x2 ?y2 ?x2g ?y4g)
    (plusplus ?x2g ?x3g)
    (plusplus ?y3g ?y4g)
    (true (cellholds ?q3 ?x3 ?y3 ?player))
    (globalindex ?q3 ?x3 ?y3 ?x3g ?y3g)
    (plusplus ?x3g ?x4g)
    (plusplus ?y2g ?y3g)
    (true (cellholds ?q4 ?x4 ?y4 ?player))
    (globalindex ?q4 ?x4 ?y4 ?x4g ?y2g)
    (plusplus ?x4g ?x5g)
    (plusplus ?y1g ?y2g)
    (true (cellholds ?q5 ?x5 ?y5 ?player))
    (globalindex ?q5 ?x5 ?y5 ?x5g ?y1g))
(<= (globalindex 1 ?x ?y ?xg ?yg)
    (plus 3 ?x ?xg)
    (plus 3 ?y ?yg))
(<= (globalindex 2 ?x ?y ?x ?yg)
    (index ?x)
    (plus 3 ?y ?yg))
(<= (globalindex 3 ?x ?y ?x ?y)
    (index ?x)
    (index ?y))
(<= (globalindex 4 ?x ?y ?xg ?y)
    (plus 3 ?x ?xg)
    (index ?y))
(<= (rotation clockwise ?x1 ?y1 ?x2 ?y2)
    (verticalflip ?y1 ?x1 ?x2 ?y2))
(<= (rotation counterclockwise ?x1 ?y1 ?x2 ?y2)
    (horizontalflip ?y1 ?x1 ?x2 ?y2))
(<= (horizontalflip 1 ?y 3 ?y)
    (index ?y))
(<= (horizontalflip 2 ?y 2 ?y)
    (index ?y))
(<= (horizontalflip 3 ?y 1 ?y)
    (index ?y))
(<= (verticalflip ?x 1 ?x 3)
    (index ?x))
(<= (verticalflip ?x 2 ?x 2)
    (index ?x))
(<= (verticalflip ?x 3 ?x 1)
    (index ?x))
(<= (cell ?q ?x ?y)
    (quadrant ?q)
    (index ?x)
    (index ?y))
(<= (cellempty ?q ?x ?y)
    (cell ?q ?x ?y)
    (not (true (cellholds ?q ?x ?y red)))
    (not (true (cellholds ?q ?x ?y black))))
(<= boardopen
    (cellempty ?q ?x ?y))
(index 1)
(index 2)
(index 3)
(quadrant 1)
(quadrant 2)
(quadrant 3)
(quadrant 4)
(direction clockwise)
(direction counterclockwise)
(plusplus 1 2)
(plusplus 2 3)
(plusplus 3 4)
(plusplus 4 5)
(plusplus 5 6)
(plus 3 1 4)
(plus 3 2 5)
(plus 3 3 6)