(role player1)
(role player2)

(init (heap a 1))
(init (heap b 5))
(init (heap c 4))
(init (heap d 2))

(init (control player1))

(<= (legal ?p noop)
    (true (control ?x))
    (role ?p)
    (distinct ?x ?p))

(<= (legal ?p (reduce ?x ?n))
    (true (control ?p))
    (true (heap ?x ?m))
    (smaller ?n ?m))

(<= (next (heap ?x ?n))
    (does ?p (reduce ?x ?n)))

(<= (next (heap ?x ?n))
    (true (heap ?x ?n))
    (does ?p (reduce ?y ?m))
    (distinct ?x ?y))

(<= (next (control ?p2))
    (true (control ?p1))
    (next_player ?p1 ?p2))

(<= terminal
    (true (heap a 0))
    (true (heap b 0))
    (true (heap c 0))
    (true (heap d 0)))

(<= (goal ?p 0)
    (true (control ?p)))
	
(<= (goal ?p 100)
    (true (control ?p1))
    (next_player ?p ?p1))
    
(<= (smaller ?x ?y)
    (succ ?x ?y))
(<= (smaller ?x ?y)
    (succ ?x ?z)
    (smaller ?z ?y))

(next_player player1 player2)
(next_player player2 player1)

(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)



